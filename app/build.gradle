plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'jacoco'
}

android {
    namespace "com.projet.sluca.smallbrother"
    compileSdkVersion 33
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "com.projet.sluca.smallbrother"
        minSdkVersion 23
        targetSdkVersion 33
        versionName '1.2'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    // autophoto ----
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // ----
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //Dependencies for Android framework and interface
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.appcompat:appcompat-resources:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    //Dependency for Location
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.5.1'

    //JUnit testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    //Espresso
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    //UI testing with Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.5.1'
    testImplementation "io.mockk:mockk:1.12.0"
    //For barcode testing
    implementation 'com.google.android.gms:play-services-vision:20.1.3'

    implementation "androidx.savedstate:savedstate-ktx:1.2.1"
    implementation "androidx.core:core-ktx:1.10.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

    //QRCODE GENERATOR
    implementation 'com.github.androidmads:QRGenerator:1.0.1'

    //QRCODE SCANNER
    implementation 'com.github.yuriy-budiyev:code-scanner:2.3.2'

    //Crypto
    implementation("androidx.security:security-crypto:1.0.0")

    //Ktor dependencies
    // HTTP engine: that handles network requests.
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"
    implementation "io.ktor:ktor-client-android:$ktor_version"
    // It is used for JSON serialization and deserialization settings and
    // is recommended for multiplatform projects
    implementation "io.ktor:ktor-client-serialization:$ktor_version"
    //It is kotlinx.serialization, which is used for entity serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1"
    //It's for logging HTTP requests
    implementation "io.ktor:ktor-client-logging-jvm:$ktor_version"
    //Content Negotiation replaces JsonFeature
    implementation("io.ktor:ktor-client-content-negotiation:$ktor_version")
    implementation("io.ktor:ktor-serialization-kotlinx-json:$ktor_version")
}
repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

//Following code found here: https://github.com/ChristopherBull/demo-merge-android-coverage
jacoco {
    toolVersion = "$jacocoVersion"
    reportsDirectory = file("$buildDir/reports/coverage")
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task jacocoCombinedTestReports(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {
    group = "Verification"
    description = "Creates JaCoCo test coverage report for Unit and Instrumented Tests (combined) on the Debug build"
    reports {
        xml.enabled = true
        html.enabled = true
    }

    // Files to exclude:
    // Generated classes, platform classes, etc.
    def excludes = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*'
    ]

    // generated classes
    classDirectories.from = fileTree(
            dir: "$buildDir/intermediates/classes/debug",
            excludes: excludes
    ) + fileTree(
            dir: "$buildDir/tmp/kotlin-classes/debug",
            excludes: excludes
    )
    // sources
    sourceDirectories.from = [
            android.sourceSets.main.java.srcDirs,
            "src/main/kotlin"
    ]
    // Output and existing data
    // Combine Unit test and Instrumented test reports
    executionData.from = fileTree(dir: "$buildDir", includes: [
            // Unit tests coverage data
            "outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec",
            // Instrumented tests coverage data
            "outputs/code_coverage/debugAndroidTest/connected/*/coverage.ec"
    ])
}